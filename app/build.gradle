apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'


def BUILD_TIME = new Date().format("MMddHHmm")
def VERSION_PRE = "0.1"
def SVN_VERSION = 11

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.gwell.iotvideodemo"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionName "${VERSION_PRE}.${SVN_VERSION}"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            //加载本地local.properties文件
            if (rootProject.file('local.properties').exists()) {
                properties.load(rootProject.file("local.properties").newDataInputStream())
                if (properties.get("storeFile") != null) {
                    storeFile file(properties.get("storeFile"))
                    storePassword properties.get("storePassword")
                    keyAlias properties.get("keyAlias")
                    keyPassword properties.get("keyPassword")
                }
            } else {
                println("IoTVideo -> buildTypes -> release: using default key")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    //定制output的apk文件名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def productName = "IoTVideo"
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 版本名称
                def versionName = defaultConfig.versionName
                def fileName = "${productName}_V${versionName}_${BUILD_TIME}.apk"
                outputFileName = fileName
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.50'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //google open source
    implementation "com.android.support:design:29.0.0"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    //third party software
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'com.airbnb.android:lottie:2.7.0'

    releaseImplementation(name: "accountmgr-release", ext: 'aar')

    //IoTVideo core
    releaseImplementation(name: "iotvideo-release", ext: 'aar')
    releaseImplementation 'com.google.zxing:core:3.3.0'
    releaseImplementation 'com.clj.fastble:FastBleLib:2.3.4'
    releaseImplementation 'com.squareup.okhttp3:okhttp:4.2.0'
    releaseImplementation 'com.squareup.retrofit2:retrofit:2.6.2'
    releaseImplementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    releaseImplementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    releaseImplementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    releaseImplementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    releaseImplementation 'com.google.code.gson:gson:2.8.5'
    releaseImplementation 'com.squareup.okhttp3:logging-interceptor:4.2.0'

    debugImplementation project(":accountmgr")
    debugImplementation project(":iotvideo")
}
